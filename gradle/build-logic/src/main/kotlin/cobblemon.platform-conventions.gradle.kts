import utilities.VersionType
import utilities.writeVersion

plugins {
    id("cobblemon.base-conventions")
    // PT011: 정밀한 타입세이프 accessor 생성을 위해 fabric-loom을 직접 명시(transitive 적용에서는 accessor 부재).
    id("net.fabricmc.fabric-loom")
    id("com.gradleup.shadow")
}

writeVersion(type = VersionType.FULL)

val bundle: Configuration by configurations.creating {
    isCanBeConsumed = false
    isCanBeResolved = true
}

loom {
    val clientConfig = runConfigs.getByName("client")
    clientConfig.runDir = "runClient"
//    clientConfig.vmArg("-Dmixin.debug=true")
    clientConfig.programArg("--username=AshKetchum")
    //This is AshKetchum's UUID so you get an Ash Ketchum skin
    clientConfig.programArg("--uuid=93e4e551-589a-41cb-ab2d-435266c8e035")
    val serverConfig = runConfigs.getByName("server")
    serverConfig.runDir = "runServer"

    // Forge established the "main" name convention we're using here. Since NeoForged already defines it we must use
    // `maybeCreate`
    mods.maybeCreate("main")
    // This configurations ensures that the code and resources in :common are available to the platform-specific builds
    // in specifically the development mode. This code does nothing for the final packed jar since that one uses shadow
    // bundling / jar-in-jar magic to make sure the platform-specific jar has all relevant files available.
    mods.named("main") {
        sourceSet(project.sourceSets.main.get())
        sourceSet(project(":common").sourceSets.main.get())
    }
}

tasks {

    jar {
        archiveBaseName.set("Cobblemon-${project.name}")
        archiveClassifier.set("dev-slim")
    }

    // PT012 (port/26.1.x): fabric-loom 1.15.5 no-remap mode → `remapJar` task 미등록.
    // shadowJar를 production jar로 직접 사용 (classifier 비움, archive name·version 명시).
    shadowJar {
        archiveClassifier.set("")
        archiveBaseName.set("Cobblemon-${project.name}")
        archiveVersion.set("${rootProject.version}")
        configurations = listOf(bundle)
        mergeServiceFiles()

        // port/26.2: GraalVM is no longer relocated here. Truffle's polyglot support binds a native
        // library whose JNI symbols follow the class name, so a renamed JDKSupport dies with
        // UnsatisfiedLinkError the moment a JS context is created - which is every battle. Upstream has
        // no fix (https://github.com/oracle/graal/issues/10907); the documented workaround of excluding
        // com.oracle.truffle.polyglot from the relocation is the fallback if another mod's Graal clashes.
        // NeoForge carries this relocation itself.
    }

    val copyJar by registering(CopyFile::class) {
        val productionJar = shadowJar.flatMap { it.archiveFile }
        fileToCopy = productionJar
        destination = productionJar.flatMap {
            rootProject.layout.buildDirectory.file("libs/${it.asFile.name}")
        }
    }

    assemble {
        dependsOn(copyJar)
    }

}
